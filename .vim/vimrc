set nocompatible              " be iMproved, required
filetype off                  " required

set rtp+=~/.vim/bundle/Vundle.vim

call vundle#begin()

Plugin 'VundleVim/Vundle.vim'
Plugin 'vim-airline/vim-airline'
Plugin 'airblade/vim-gitgutter'
Plugin 'junegunn/fzf'
Plugin 'junegunn/fzf.vim'
Plugin 'menisadi/kanagawa.vim'

call vundle#end()            " required

filetype plugin indent on    " required
" To ignore plugin indent changes, instead use:
"filetype plugin on

"
" Brief help
" :PluginList       - lists configured plugins
" :PluginInstall    - installs plugins; append `!` to update or just :PluginUpdate
" :PluginSearch foo - searches for foo; append `!` to refresh local cache
" :PluginClean      - confirms removal of unused plugins; append `!` to auto-approve removal
"
" see :h vundle for more details or wiki for FAQ
" Put your non-Plugin stuff after this line

"###################"
" Non vundle config "
"###################"

set expandtab tabstop=4 softtabstop=4 shiftwidth=4 smartindent

" UI
set number
set mouse=a
set ruler
set nowrap
set colorcolumn=80
set nocursorline

" Search
set incsearch
set hlsearch
" Remove highlights
nnoremap <Esc><Esc> :nohlsearch<CR>

" Editing
set noerrorbells
set backspace=2
set encoding=utf-8
set completeopt=menuone,noinsert,noselect

" Display useless whitespaces
set list
set listchars=tab:»·,trail:·
highlight SpecialKey ctermfg=darkred

" `gf` opens file under cursor in a new vertical split
nnoremap gf :vertical wincmd f<CR>

" Headers as C files
let g:c_syntax_for_h = 1

" FZF – Quick Open
nnoremap <C-p> :Files<CR>

set termguicolors
colorscheme kanagawa
syntax on
set background=dark

" Tab / Shift-Tab completion in Insert mode
function! s:TabComplete() abort
  if pumvisible()
    return "\<C-n>"
  endif
  if getline('.')[0 : col('.')-2] =~# '^\s*$'
    return "\<Tab>"
  endif
  return "\<C-n>"
endfunction
inoremap <expr> <Tab> <SID>TabComplete()
inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"

" Rust: format current buffer with rustfmt on save
augroup RustFmtCurrentFile
  autocmd!
  autocmd BufWritePre *.rs call RustFmtCurrentBuffer()
augroup END

function! RustFmtCurrentBuffer() abort
  let l:view = winsaveview()
  let l:src = join(getline(1, '$'), "\n") . "\n"
  let l:cmd = 'rustfmt --emit=stdout --edition=2024 --style-edition=2024'
  let l:out = system(l:cmd, l:src)

  if v:shell_error != 0
    echohl ErrorMsg
    echom 'rustfmt failed:'
    for l:line in split(l:out, "\n")
      if !empty(l:line)
        echom l:line
      endif
    endfor
    echohl None
    return
  endif

  let l:lines = split(l:out, "\n", 1)
  if !empty(l:lines) && l:lines[-1] ==# ''
    call remove(l:lines, -1)
  endif

  keepjumps call setline(1, l:lines)
  if line('$') > len(l:lines)
    execute (len(l:lines) + 1) . ',$delete _'
  endif
  call winrestview(l:view)
endfunction
